// TODO: clear execution section after memory inserted

// Enqueue whole program, if queue is empty
if(!'data entity @s HandItems[0].tag.execution.queue[]') {
    /data modify entity @s HandItems[0].tag.execution.to_enqueue set from entity @s HandItems[0].tag.compiled
    /function computercraft:machines/robot/enqueue/main
}
 
// Parse rotation
/execute as @e[tag=CC_Robot_Body] at @s store result score @s currentRotation run data get entity @s Rotation[0]
/scoreboard players operation @e[tag=CC_Robot_Body] currentRotation += CC maxAngle
/scoreboard players operation @e[tag=CC_Robot_Body] currentRotation %= CC maxAngle

// Clear own block
/execute as @e[tag=CC_Robot_Body] at @s run setblock ~ ~2 ~ minecraft:air destroy

// Check for empty program
if('data entity @s HandItems[0].tag.execution.queue[]') {
    /data modify entity @s HandItems[0].tag.execution.temp set from entity @s HandItems[0].tag.execution.queue[0]
} else {
    /data modify entity @s HandItems[0].tag.execution.temp set value {}
}

// Remove first item from queue
/data remove entity @s HandItems[0].tag.execution.queue[0]

// --------------- Execute commands ----------------

modal cmd_robot(id, funct) {
    if('data entity @s HandItems[0].tag.execution.temp{id:$(id)}') {
        /function computercraft:machines/robot/cmd/$(funct)
    }
}

// Robot commands
if('data entity @s HandItems[0].tag.execution.temp{type:"cmd_robot"}') {
    cmd_robot("forward", "fwd")
    cmd_robot("right", "right")
    cmd_robot("left", "left")
    cmd_robot("dig", "mine")
    cmd_robot("up", "up")
    cmd_robot("down", "down")
    cmd_robot("back", "back")
    cmd_robot("dig_up", "mine_up")
    cmd_robot("dig_down", "mine_down")
    cmd_robot("hit", "hit")
}

// If statement
if('data entity @s HandItems[0].tag.execution.temp{type:"control",id:"if"}') {
    // TODO: check for condition
    detect_block(0,1,0,"block_front")
    /execute store success entity @s HandItems[0].tag.execution.block_not_match byte 1 run data modify entity @s HandItems[0].tag.execution.var.block_front set from entity @s HandItems[0].tag.execution.temp.condition[0].id
    
    if ('data entity @s HandItems[0].tag.execution{block_not_match:0b}') {
        /data modify entity @s HandItems[0].tag.execution.to_enqueue set from entity @s HandItems[0].tag.execution.temp.then
    } else {
        /data modify entity @s HandItems[0].tag.execution.to_enqueue set from entity @s HandItems[0].tag.execution.temp.else
    }
    
    /function computercraft:machines/robot/enqueue/main
}